plugins {
    id 'java'
    id 'java-library'
    id 'maven-publish'
    id 'io.github.karlatemp.publication-sign' version '1.3.7'
}

allprojects {
    group 'io.github.kasukusakura'
    version '0.0.2'
}

repositories {
    mavenCentral()
}

dependencies {
    testImplementation 'io.github.karlatemp:unsafe-accessor:1.6.2'
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.6.0'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine'
    testImplementation fileTree('tm')
    api 'org.jetbrains:annotations:22.0.0'
}

test {
    useJUnitPlatform()
}

compileJava {
    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8
}

tasks.create('packNativeLibs')


tasks.create("sourcesJar", Jar.class) { tsk ->
    tsk.dependsOn("classes")
    tsk.archiveClassifier.set('sources')
    tsk.from project.sourceSets.main.allSource
}

tasks.create("javadocJar", Jar.class) { tsk ->
    tsk.archiveClassifier.set('javadoc')
}

publishing {
    repositories {
        maven {
            setName("MavenCentral")
            // Maven Central
            setUrl("https://s01.oss.sonatype.org/service/local/staging/deploy/maven2")

            credentials {
                def prop = { name ->
                    if (rootProject.hasProperty(name)) {
                        return rootProject.property(name)
                    }
                    return ""
                }
                username = prop('sonatype.user')
                password = prop('sonatype.key')
            }

            metadataSources.ignoreGradleMetadataRedirection()
        }

        mavenLocal {
            metadataSources.ignoreGradleMetadataRedirection()
        }

    }

    publications { container ->
        register("java", MavenPublication.class) { publication ->
            publication.artifact(tasks.findByName('jar'))
            publication.artifact(tasks.findByName('sourcesJar'))
            publication.artifact(tasks.findByName('javadocJar'))
            publication.setModuleDescriptorGenerator(null)
            publication.artifactId('silk-codec')

            publication.pom {
                name = 'Silk codec'
                url = 'https://github.com/KasukuSakura/silk-codec'
                description = 'silk codec'
                licenses {
                    license {
                        name = 'Apache 2.0 License'
                        url = 'https://www.apache.org/licenses/LICENSE-2.0'
                    }
                }
                developers {
                    developer {
                        name = 'Karlatemp'
                        email = 'karlatemp@vip.qq.com'
                    }
                }
                scm {
                    url = 'https://github.com/KasukuSakura/silk-codec.git'
                    connection = 'https://github.com/KasukuSakura/silk-codec.git'
                }
            }
        }
    }
}

publicationSign {
    setupWorkflow { workflow ->
        workflow.fastSetup("keys/key.pub", "keys/key.pri")
    }
}
